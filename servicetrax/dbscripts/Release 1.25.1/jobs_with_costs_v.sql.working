DROP VIEW jobs_with_costs_v
GO

CREATE view jobs_with_costs_v AS 
SELECT TOP 100 PERCENT o.organization_id, 
       o.name as [Organization Name], 
       j.job_no AS [Job No],	
       l.name AS [Job Type], 
       cust.customer_name AS [Customer Name], 
       cust.dealer_name AS [Dealer Name], 
       u.first_name + ' ' + u.last_name AS [Job Owner], 
       inv.invoice_id AS [Invoice No], 
       inv.date_sent AS [Invoiced Date], 
       r.name AS [Job Supervisor], 
       c.contact_name AS [SP Sales], 
       SUM(ISNULL(il.extended_price, 0)) AS [Total Invoiced],
       (SELECT ISNULL(SUM(sl.bill_qty * ISNULL(i.cost_per_uom, 0)), 0)
          FROM service_lines sl INNER JOIN
               items i ON sl.item_id = i.item_id
         WHERE sl.invoice_id = inv.invoice_id 
           AND sl.tc_job_id = j.job_id 
           AND sl.item_type_code = 'hours' 
           AND i.name NOT LIKE 'TRUCK%' 
           AND (i.name NOT LIKE 'sub%' OR i.name LIKE 'sub%exp%')) AS [Labor Cost],
       (SELECT ISNULL(SUM(sl.bill_qty * ISNULL(i.cost_per_uom, 0)), 0)
          FROM service_lines sl INNER JOIN
               items i ON sl.item_id = i.item_id
         WHERE sl.invoice_id = inv.invoice_id 
           AND sl.tc_job_id = j.job_id 
           AND sl.item_type_code = 'hours' 
           AND i.name LIKE 'TRUCK%') AS [Truck Cost ],
       (SELECT ISNULL(SUM(sl.bill_qty * ISNULL(i.cost_per_uom, 0)), 0)
          FROM service_lines sl INNER JOIN
               items i ON sl.item_id = i.item_id
         WHERE sl.invoice_id = inv.invoice_id 
           AND sl.tc_job_id = j.job_id 
           AND sl.item_type_code = 'hours' 
           AND i.name LIKE 'SUB-%' 
           AND i.name NOT LIKE 'SUB%EXP%') AS [Sub Cost ],
        (SELECT ISNULL(SUM(CASE WHEN sl.entry_method = 'great plains' THEN sl.expense_total
                ELSE (sl.bill_qty * i.cost_per_uom) END), 0)
          FROM service_lines sl INNER JOIN
               items i ON sl.item_id = i.item_id
         WHERE sl.invoice_id = inv.invoice_id 
           AND sl.tc_job_id = j.job_id 
           AND sl.item_type_code = 'expense') AS [Expense Cost]
  FROM dbo.jobs j INNER JOIN 
       dbo.lookups l ON j.job_type_id=l.lookup_id INNER JOIN 
       dbo.lookup_types lt ON l.lookup_type_id=lt.lookup_type_id INNER JOIN
       dbo.invoices inv ON j.job_id = inv.job_id LEFT OUTER JOIN
       dbo.invoice_lines il ON inv.invoice_id = il.invoice_id INNER JOIN
       dbo.customers cust ON j.customer_id = cust.customer_id INNER JOIN
       dbo.organizations o ON cust.organization_id = o.organization_id LEFT OUTER JOIN
       dbo.resources r ON j.foreman_resource_id = r.resource_id LEFT OUTER JOIN
       dbo.users u ON j.billing_user_id = u.user_id LEFT OUTER JOIN
       dbo.contacts c ON j.a_m_sales_contact_id = c.contact_id
 WHERE lt.code='job_type'
GROUP BY o.name, 
         j.job_no, 
         l.name,
         cust.dealer_name, 
         u.first_name + ' ' + u.last_name, r.name, 
         inv.invoice_id, inv.date_sent, 
         o.organization_id, cust.customer_name, 
         c.contact_name, j.job_id
ORDER BY o.name, 
         j.job_no
GO